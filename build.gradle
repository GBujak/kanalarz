plugins {
	id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'com.vanniktech.maven.publish' version "0.36.0"
    id 'io.spring.dependency-management' version '1.1.7'
	id 'org.springframework.boot' version '4.0.2'
}

group = 'com.gbujak'
version = System.getenv("RELEASE_VERSION") ?: '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jspecify:jspecify'
    implementation 'org.slf4j:slf4j-api'
    implementation 'org.springframework:spring-context'
    implementation 'com.github.f4b6a3:uuid-creator:6.1.1'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation "com.fasterxml.jackson.core:jackson-databind"
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"
	testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
    beforeTest { descriptor ->
        logger.lifecycle("${descriptor}")
    }
    testLogging {
        outputs.upToDateWhen {false}
    }
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    coordinates("com.gbujak", "kanalarz", System.getenv("RELEASE_VERSION"))

    pom {
        name = "Kanalarz"
        description = "A simple Spring rollback library."
        inceptionYear = "2025"
        url = "https://github.com/gbujak/kanalarz/"
        licenses {
            license {
                name = "MIT License"
                url = "https://opensource.org/license/mit"
                distribution = "https://opensource.org/license/mit"
            }
        }
        developers {
            developer {
                id = "gbujak"
                name = "Grzegorz Bujak"
                url = "https://github.com/gbujak/"
            }
        }
        scm {
            url = "https://github.com/gbujak/kanalarz/"
            connection = "scm:git:git://github.com/gbujak/kanalarz.git"
            developerConnection = "scm:git:ssh://git@github.com/gbujak/kanalarz.git"
        }
    }
}
